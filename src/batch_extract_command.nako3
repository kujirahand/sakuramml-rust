# ------------------------------------------
# extract command
# ------------------------------------------
SCRIPT_DIR=母艦パス
ROOT_DIR=SCRIPT_DIRからパス抽出
「ROOT=」&ROOT_DIRを表示
# ------------------------------------------
RES=""
# ------------------------------------------
# lexer.rs
# ------------------------------------------
L=「{SCRIPT_DIR}/lexer.rs」を開く。
Lから「<CHAR_COMMANDS>」を切り取る。
CMD_LIST=対象から「</CHAR_COMMANDS>」を切り取る。
CMD_LIST=CMD_LIST&改行。
R=""
CMD_LISTを改行で区切って反復：
　　対象で「// @ 」が何文字目。
　　もし、それ=0ならば続ける。
　　C=対象；
　　Cから「=>」を切り取る。
　　トリムして「|」を「 」に置換して、「'」を「」に置換して、Fコマンドに代入。
　　Cを「// @」で区切る。
　　それ[1]をトリムしてF説明に代入。
　　R=R&「| {Fコマンド} | {F説明} |」&改行。
RES_1CHAR=R
# ------------------------------------------
# ストトン表記 - sutoton.rs
# ------------------------------------------
R=""
CMD_LIST=""
L=「{SCRIPT_DIR}/sutoton.rs」を開く。
Lの「<SUTOTON>」から「</SUTOTON>」を範囲切り取る。
S_LISTに代入。
S_LISTを改行で区切って反復
　　S=対象。
　　Ｓで「items.set_item」が何文字目。
　　もし、それ＝0ならば、続ける。
　　それはＳ。
　　Sを「// @」で区切ってITに代入。
　　S説明＝(IT[1])をトリム
　　それはIT[0]
　　「items.set_item(」を「」に置換
　　「);」を「」に置換。
　　トリム
　　Ｓ＝それ
　　SS=Ｓを「,」で区切る。
　　Ｓ名前＝トリム（SS[0]）
　　Ｓ定義＝トリム(ＳＳ[1])
　　Ｓ＝S名前＆「=> // @ {S説明}(={S定義}) 」
　　# Ｓを表示。
　　CMD_LIST=CMD_LIST&S&改行。
　　R=R&「| {S名前} | {S定義} | {S説明} |」&改行
ここまで。
RES_SUTOTON=「
| ストトン表記 | 定義  | 説明 |
| ---------|---------|--------|
{R}
」
# ------------------------------------------
# 音色 - mml_def.rs
# ------------------------------------------
RES=""
L=「{SCRIPT_DIR}/mml_def.rs」を開く。
Lの「<VARIABLES>」から「</VARIABLES>」まで範囲切り取って、S_LISTに代入。
S_LISTを改行で区切って反復
　　対象を『/"(.+?)".*\((\d+|\".+?\").*\/\/ \@(.+?)$/』で正規表現マッチ。
　　もし、そうでなければ、続ける。
　　KEY=抽出文字列[0]
　　VAL=抽出文字列[1]
　　DESC=抽出文字列[2]
　　RES=RES&「| {KEY} | {DESC} (値:{VAL}) |」&改行。
ここまで。
RES_VOICE=RES
# ------------------------------------------
RES=""
Lの「<RHYTHM_MACRO>」から「</RHYTHM_MACRO>」を範囲切り取る。S_LISTに代入。
S_LISTを改行で区切って反復
　　対象を『/'(.)'.*:from\((".+?")/』で正規表現マッチ。
　　もし、そうでなければ、続ける。
　　KEY=抽出文字列[0]
　　VAL=抽出文字列[1]
　　RES=RES&「| {KEY} | {VAL} |」&改行。
ここまで。
RES_RHYTHM_MACRO=RES
# ------------------------------------------
RES=""
Lの「<SYSTEM_FUNCTION>」から「</SYSTEM_FUNCTION>」を範囲切り取る。S_LISTに代入。
S_LISTを改行で区切って反復
　　対象を『/"(.+)".*\/\/\s+(.*)/』で正規表現マッチ。
　　もし、そうでなければ、続ける。
　　KEY=抽出文字列[0]
　　VAL=抽出文字列[1]
　　RES=RES&「| {KEY} | {VAL} |」&改行。
ここまで。
RES_UP=RES

# ------------------------------------------
# 参照できるシステムの値 - runner.rs
# ------------------------------------------
RES=""
L=「{SCRIPT_DIR}/runner.rs」を開く。
Lの「<SYSTEM_REF>」から「</SYSTEM_REF>」を範囲切り取る。S_LISTに代入。
S_LISTを改行で区切って反復
　　対象で「if cmd ==」が何文字目。
　　もし、それが0ならば、続ける。
　　対象の「if cmd ==」を「|」に置換。
　　「cmd ==」を「」に置換。
　　「"」を「」に置換。
　　『{』を「」に置換。
　　「||」を「/」に置換。
　　「// @ 」を「|」に置換。
　　トリム
　　S=それ。
　　RES=RES&S&"|"&改行。
ここまで。
RES_REF=RES

# ------------------------------------------
●(CMD_LISTを)コマンド変換処理とは
　　RESとは変数＝「」
　　CMD_LISTを改行で区切って反復
　　　　トリム。LINE=それ
　　　　LINEで「// @」が何文字目
　　　　もし、それ＝0ならば、続ける。
　　　　LINEから「=>」を切り取って、CMDに代入。
　　　　対象から「// @」を切り取る。
　　　　説明＝トリム(対象)。
　　　　CMD=CMDの「||」を「 / 」に置換。
　　　　CMD=CMDの「'\''」を「’」に置換。
　　　　CMD=CMDの「'|'」を「｜」に置換。
　　　　CMD=CMDの「|」を「 / 」に置換。
　　　　CMD=CMDの『\s{2,}』を「 」に正規表現置換。
　　　　CMD=CMDの「'」を「」に置換。
　　　　CMD=CMDの「"」を「」に置換。
　　　　CMD=トリム(CMD)
　　　　RES=RES&「| {CMD} | {説明} |」&改行
　　ここまで。
　　ＲＥＳを戻す。
ここまで。

MD=「
# Sakuramml command list - テキスト音楽 サクラ

## One character command - 1文字コマンド

Lower character command list.

| Command | Description |
|---------|--------|
{RES_1CHAR}

## Multi characters command - 複数文字の大文字コマンド

Upper characters command list.

| Command | Description |
|---------|--------|
{RES_UP}

## Variables - 計算式で参照できる値

| Command | Description |
|---------|--------|
{RES_REF}

## Macro and Voice List (マクロや音色など変数定義)

| Voice | Description |
|-------|----|
{RES_VOICE}

## Rhythm macro (リズムマクロ)

| Macro's name | Value |
|---------|--------|
{RES_RHYTHM_MACRO}

## Sutoton - ストトン表記

日本語で指示できるコマンド

{RES_SUTOTON}
」

# command.md
MD=トリム(MD)
MDを表示。

MDを「{ROOT_DIR}/command.md」に保存。
「ok. {今}」と表示。
